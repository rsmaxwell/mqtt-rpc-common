
plugins {
    id 'java-library'
    id 'maven-publish'
    id 'eclipse'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation libs.junit.jupiter
    testRuntimeOnly libs.junit.platform.launcher
    testImplementation libs.log4j.core

    compileOnly libs.lombok
    annotationProcessor libs.lombok

    implementation libs.guava
    implementation libs.org.eclipse.paho.mqttv5.client
    implementation libs.log4j.api
    implementation libs.jackson.databind
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    withSourcesJar()
}

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

tasks.named('test') {
    useJUnitPlatform()
}

ext {
    group = project.findProperty('projectGroupId') ?: 'com.rsmaxwell.mqtt.rpc'
    version = project.findProperty('projectVersion') ?: '0.0.0-SNAPSHOT'

    repositoryName = findProperty('repositoryName') ?: repositoryName_default
    projectVersion = findProperty('projectVersion') ?: version_default
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = project.findProperty('projectArtifactId') ?: project.name
            groupId    = project.group.toString()
            version    = project.version.toString()
            from components["java"]
        }
    }

    repositories {
        maven {
            credentials {
                username = mavenUser
                password = mavenPassword
            }

            url = "${mavenRepositoryBaseURL}/${repositoryName}"
            allowInsecureProtocol = true
        }
    }
}

